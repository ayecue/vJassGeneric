library Deferred    
    private struct State extends array
        implement InstanceHandler
        
        private trigger tCallback
        private Deferred dContext
        
        static method get takes Deferred ctx returns thistype
            local thistype new = thistype.allocate()
            
            set new.tCallback = CreateTrigger()
            set new.dContext = ctx
            
            return new
        endmethod
        
        method add takes code fn returns Deferred
            call TriggerAddCondition(this.tCallback,Filter(fn))
        
            return this.dContext
        endmethod
        
        method execute takes nothing returns nothing
            set Deferred.Context = this.dContext
            call TriggerEvaluate(this.tCallback)
        endmethod
        
        method remove takes nothing returns nothing
            call DestroyTrigger(this.tCallback)
            
            set this.tCallback = null
        endmethod
    endstruct

    struct Deferred extends array 
        static thistype Context = -1
    
        implement InstanceHandler
    
        private State sSuccess   
        private State sFailure
        private State sAlways
        private thistype oNext
        
        static method operator new takes nothing returns thistype
            local thistype new = thistype.allocate()
            
            set new.sSuccess = State.get(new)
            set new.sFailure = State.get(new)
            set new.sAlways = State.get(new)
            set new.oNext = -1
            
            return new
        endmethod
        
        method next takes nothing returns thistype
            if (this.oNext == -1) then
                set this.oNext = thistype.new
            endif
            
            return this.oNext
        endmethod
        
        method done takes code fn returns thistype
            return sSuccess.add(fn)
        endmethod
        
        method fail takes code fn returns thistype
            return sFailure.add(fn)
        endmethod
        
        method always takes code fn returns thistype
            return sAlways.add(fn)
        endmethod
        
        method resolve takes nothing returns nothing
            call this.sSuccess.execute()
            call this.sAlways.execute()
        endmethod
        
        method reject takes nothing returns nothing
            call this.sFailure.execute()
            call this.sAlways.execute()
        endmethod
        
        method destroy takes nothing returns nothing
            call this.sSuccess.remove()
            call this.sFailure.remove()
            call this.sAlways.remove()
        endmethod
    endstruct
endlibrary